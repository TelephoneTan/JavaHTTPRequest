plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'org.example'
version '1.0-SNAPSHOT'

apply from: "./local.build.gradle"

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/TelephoneTan/JavaHTTPRequest")
            credentials {
                username = project.ext.gpr_github_user ?: System.getenv("GITHUB_USERNAME")
                password = project.ext.gpr_github_key ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'pub.telephone'
            artifactId = 'java-http-request'
            version = '7.0.0'

            from(components.java)
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/TelephoneTan/JavaPromise")
        credentials {
            username = project.ext.gpr_github_user ?: System.getenv("GITHUB_USERNAME")
            password = project.ext.gpr_github_key ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

sourceCompatibility = 1.8

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    api 'com.squareup.okhttp3:okhttp:5.0.0-alpha.12'
    api 'org.json:json:20231013'
    api 'org.jsoup:jsoup:1.15.3'
    api 'pub.telephone:java-promise:3.2.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}